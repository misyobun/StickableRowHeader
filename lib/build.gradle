apply plugin: 'maven'
apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

uploadArchives {
    repositories {
        mavenDeployer {
            repository url: "./rep/"
            pom.version = "1.0.1"
            pom.groupId = "msb.jp.stickablerowheader"
            pom.artifactId = "msb-stickable-row-header"
        }
    }
}

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }

}

group = 'msb.jp.stickablerowheader'
version = '1.0.1'
def siteUrl = 'https://github.com/misyobun/StickableRowHeader'
def gitUrl = 'https://github.com/misyobun/StickableRowHeader.git'

bintray {
    def properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    user = properties.getProperty("bintray_user")
    key = properties.getProperty("bintray_api_key")

    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = 'StickableRowHeader'
        desc = 'Like to be UItableView Section .'
        websiteUrl = siteUrl
        issueTrackerUrl = 'https://github.com/misyobun/StickableRowHeader/issues'
        vcsUrl = gitUrl
        licenses = ['MIT']
        labels = ['android']
        publicDownloadNumbers = true
    }
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'StickableRowHeader'
                url siteUrl
                licenses {
                    license {
                        name 'MIT License'
                        url 'http://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id 'misyobun'
                        name 'Naoto Takahashi'
                        email 'naoto0101@gmail.com'
                    }
                }
                scm {
                    connection 'https://github.com/misyobun/StickableRowHeader.git'
                    developerConnection 'https://github.com/misyobun/StickableRowHeader.git'
                    url siteUrl
                }
                dependencies {
                }
            }
        }
    }
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.google.android.material:material:$rootProject.supportLibraryVersion"
    implementation "androidx.appcompat:appcompat:$rootProject.supportLibraryVersion"
    implementation "androidx.core:core-ktx:$rootProject.ktxVersion"
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.0-beta02'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0-beta02'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

tasks.withType(Javadoc).all {
    enabled = false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

task findConventions << {
    println project.getConvention()
}
repositories {
    mavenCentral()
}